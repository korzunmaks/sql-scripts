USE RIASSE

SET NOCOUNT ON

IF OBJECT_ID(N'tempdb..#РасходныеНакладные', N'U') IS NOT NULL 
DROP TABLE #РасходныеНакладные

IF OBJECT_ID(N'tempdb..#РасходныеНакладныеИСчетфактура', N'U') IS NOT NULL 
DROP TABLE #РасходныеНакладныеИСчетфактура

SELECT
	'Расходная накладная' AS ТипДокументов,
	Бух_Документ_РасходнаяНакладная.ФирмаIDD AS ФирмаIDD,
	Бух_Документ_РасходнаяНакладная.IDD AS IDD,
	Бух_Документ_РасходнаяНакладная.НомерДок AS НомерДок,
	Бух_Документ_РасходнаяНакладная.ДатаДок AS ДатаДок,
	CAST(Бух_Документ_РасходнаяНакладная.closed AS INT) AS closed,
	CAST(Бух_Документ_РасходнаяНакладная.ismark AS INT) AS ismark,
	'O1'+'  BE'+Бух_Документ_РасходнаяНакладная.iddoc AS ИдентификаторРодителя, 
	CASE 
	    WHEN CAST(Бух_Документ_РасходнаяНакладная.closed AS INT) = 0
		    AND CAST(Бух_Документ_РасходнаяНакладная.ismark AS INT) = 0 
		THEN CAST(1 AS INT) 
	ELSE CAST(0 AS INT)  
	END AS OnlySaved,
	Бух_Документ_РасходнаяНакладная.Сумма AS СуммаДокумента,
	Бух_Документ_РасходнаяНакладная.Комментарий AS Комментарий	
INTO #РасходныеНакладные
FROM 
	dbo.Бух_Документ_РасходнаяНакладная AS Бух_Документ_РасходнаяНакладная with (forceseek)
	LEFT JOIN Бух_Документ_Счет_фактура
		ON Бух_Документ_РасходнаяНакладная.IDD = Бух_Документ_Счет_фактура.ДокументОснованиеIDD
	LEFT JOIN Бух_Справочник_МестаХранения AS МестаХранения with (forceseek)
		ON (Бух_Документ_РасходнаяНакладная.СкладID = МестаХранения.ID)
	LEFT JOIN Бух_Справочник_Подразделения AS ПодразделенияТорговли with (forceseek)
		ON МестаХранения.ВладелецID = ПодразделенияТорговли.ID
WHERE
	Бух_Документ_РасходнаяНакладная.ВидРасходаТабл <> 3	      --Тара
	AND Бух_Документ_РасходнаяНакладная.ВидРасходаТабл <> 4	  --Бонус
	AND Бух_Документ_РасходнаяНакладная.ВидРасходаТабл <> 9   --Только остатки
	AND Бух_Документ_РасходнаяНакладная.ВидРасходаТабл <> 10  --Товары интернет магазина
	AND Бух_Документ_РасходнаяНакладная.IDD <> ''
	AND Бух_Документ_РасходнаяНакладная.date_time_iddoc >= '20190401'  AND Бух_Документ_РасходнаяНакладная.date_time_iddoc <= '20190430z'
	--AND Бух_Документ_РасходнаяНакладная.ФирмаIDD IN ('99000050003469724')
	AND (Бух_Документ_РасходнаяНакладная.closed <> 0
			OR Бух_Документ_РасходнаяНакладная.ismark <> 0
		)

CREATE INDEX ИдентификаторРодителяИндекс ON #РасходныеНакладные (ИдентификаторРодителя)

SELECT 
	РасходныеНакладные.*
		, ISNULL(ВыданныеСчетфактурыIDD.СчетФактураIDD, '') AS СчетФактураIDD
		, ISNULL(ВыданныеСчетфактурыIDD.СчетФактураНомер, '') AS СчетФактураНомер
		, ISNULL(ВыданныеСчетфактурыIDD.СчетФактураДата, '') AS СчетФактураДата
		, ISNULL(ВыданныеСчетфактурыIDD.СчетФактураClosed, '') AS СчетФактураClosed
		, ISNULL(ВыданныеСчетфактурыIDD.СчетФактураIsmark, '') AS СчетФактураIsmark
		, ISNULL(ВыданныеСчетфактурыIDD.СчетФактураOnlySaved, '') AS СчетФактураOnlySaved
		, ISNULL(ВыданныеСчетфактурыIDD.СчетФактураСуммаДокумента, 0) AS СчетФактураСуммаДокумента
		, ISNULL(ВыданныеСчетфактурыIDD.СчетФактураКомментарий, '') AS СчетФактураКомментарий 
INTO #РасходныеНакладныеИСчетфактура
FROM 
	#РасходныеНакладные AS РасходныеНакладные
OUTER APPLY
	(SELECT TOP (1) 
		ВыданныеСчетфактуры.IDD AS СчетФактураIDD
		, ВыданныеСчетфактуры.НомерДок AS СчетФактураНомер
		, ВыданныеСчетфактуры.ДатаДок AS СчетФактураДата
		, CAST(ВыданныеСчетфактуры.closed AS INT) AS СчетФактураClosed
		, CAST(ВыданныеСчетфактуры.ismark AS INT) AS СчетФактураIsmark
		, CASE 
			WHEN CAST(ВыданныеСчетфактуры.closed AS INT) = 0
					AND CAST(ВыданныеСчетфактуры.ismark AS INT) = 0 
			THEN CAST(1 AS INT) 
			ELSE CAST(0 AS INT)  
		  END AS СчетФактураOnlySaved
		, ВыданныеСчетфактуры.Сумма AS СчетФактураСуммаДокумента
		, ВыданныеСчетфактуры.Комментарий AS СчетФактураКомментарий 
	FROM
		_1SCRDOC As ТаблицаПодчиненности with (forceseek)
		 LEFT JOIN dbo.Бух_Документ_Счет_фактура AS ВыданныеСчетфактуры with (forceseek)
			ON ТаблицаПодчиненности.CHILDID = ВыданныеСчетфактуры.iddoc
	WHERE
		ТаблицаПодчиненности.MDID = 0 -- только документы, без граф отбора
		AND ТаблицаПодчиненности.PARENTVAL = РасходныеНакладные.ИдентификаторРодителя
		--AND ВыданныеСчетфактуры.CLOSED &1=1 -- Проведенность нет смысла проверять здесь, контроль будет в отчете 
	ORDER BY 
		ТаблицаПодчиненности.CHILD_DATE_TIME_IDDOC desc
	) AS ВыданныеСчетфактурыIDD

SELECT
	Расходные.ТипДокументов AS ТипДокументов
	, Расходные.ФирмаIDD AS ФирмаIDD
	, Расходные.IDD AS IDD
	, Расходные.НомерДок AS НомерДок
	, Расходные.ДатаДок AS ДатаДок
	, Расходные.closed AS closed
	, Расходные.ismark AS ismark
	, Расходные.OnlySaved AS OnlySaved
	, Расходные.СуммаДокумента AS СуммаДокумента
	, Расходные.Комментарий AS Комментарий		
FROM
	#РасходныеНакладныеИСчетфактура AS Расходные
	
UNION ALL

SELECT
	'СчетФактура выданный' AS ТипДокументов
	, Счетфактуры.ФирмаIDD AS ФирмаIDD
	, Счетфактуры.СчетФактураIDD AS IDD
	, Счетфактуры.СчетФактураНомер AS НомерДок
	, Счетфактуры.СчетФактураДата AS ДатаДок
	, Счетфактуры.СчетФактураclosed AS closed
	, Счетфактуры.СчетФактураismark AS ismark
	, Счетфактуры.СчетФактураOnlySaved AS OnlySaved
	, Счетфактуры.СчетФактураСуммаДокумента AS СуммаДокумента
	, Счетфактуры.СчетФактураКомментарий AS Комментарий		
FROM
	#РасходныеНакладныеИСчетфактура AS Счетфактуры
WHERE
	Счетфактуры.СчетФактураIDD <> ''

IF OBJECT_ID(N'tempdb..#РасходныеНакладные', N'U') IS NOT NULL 
DROP TABLE #РасходныеНакладные

IF OBJECT_ID(N'tempdb..#РасходныеНакладныеИСчетфактура', N'U') IS NOT NULL 
DROP TABLE #РасходныеНакладныеИСчетфактура